//-----------------------------------------------------------------------------
// Установка параметров сессии
//-----------------------------------------------------------------------------
Процедура УстановкаПараметровСеанса()
	// SMS управление  инициализации

	// Проверка первого запуска
	УстПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	УстСотрудник = Справочники.Сотрудники.НайтиПоКоду(УстПользователь.УникальныйИдентификатор);
	// Если это новый сотрудник, то добавим его в базу данных
	Если НЕ ЗначениеЗаполнено(УстСотрудник) тогда
		НоваяЗаписьСпр = Справочники.Сотрудники.СоздатьЭлемент();
		НоваяЗаписьСпр.Код = УстПользователь.УникальныйИдентификатор;
		НоваяЗаписьСпр.Наименование = УстПользователь.ПолноеИмя;
		НоваяЗаписьСпр.Записать();
		УстСотрудник = НоваяЗаписьСпр.Ссылка;
		слСообщение = "Зарегистрирован новый сотрудник <" + СокрЛП(УстСотрудник.Наименование) + ">! Необходимо установить его параметры."; 
		ЗаписьЖурналаРегистрации("Программа.Запуск", УровеньЖурналаРегистрации.Информация, УстСотрудник.Метаданные(), УстСотрудник, слСообщение);
		ПараметрыСеанса.ТекПользователь = УстСотрудник;
		ПараметрыСеанса.ТекущаяГостиница = УстСотрудник.Гостиница;
		cmSendMessageToEmployee(УстСотрудник, слСообщение);
	Иначе
		Если УстСотрудник.ПометкаУдаления Тогда
			ВызватьИсключение("Сотрудник помечен на удаление!");
		КонецЕсли;
		ПараметрыСеанса.ТекПользователь = УстСотрудник;
		ПараметрыСеанса.ТекущаяГостиница = УстСотрудник.Гостиница;
	КонецЕсли;
	// Текущая рабочая станция
	слКомпьютер = ИмяКомпьютера();
	слСомпьютерСсылка = Справочники.РабочиеМеста.НайтиПоКоду(слКомпьютер);
	// Если это новая рабочая станция то добавим в базу
	Если НЕ ЗначениеЗаполнено(слСомпьютерСсылка) Тогда
		слСпрРМ = Справочники.РабочиеМеста.СоздатьЭлемент();
		слСпрРМ.Код = слКомпьютер;
		слИнфо = Новый СистемнаяИнформация;
		слСпрРМ.СистемИнфо = "Процессор: " + слИнфо.Процессор + "; ОЗУ: " + слИнфо.ОперативнаяПамять + "Mb; Версия ОС: " + слИнфо.ВерсияОС;
		слСпрРМ.Записать();
		слСомпьютерСсылка = слСпрРМ.Ссылка;
		слСообщение = "Зарегистрировано новое рабочее место <" + СокрЛП(слСомпьютерСсылка.Код) + ">! Необходимо установить его параметры."; 
		ЗаписьЖурналаРегистрации("Программа.Запуск", УровеньЖурналаРегистрации.Информация, слСомпьютерСсылка.Метаданные(), слСомпьютерСсылка, слСообщение);
		cmSendMessageToEmployee(УстСотрудник, слСообщение);
	КонецЕсли;
	Если ЗначениеЗаполнено(УстСотрудник) Тогда
		Если ЗначениеЗаполнено(УстСотрудник.РабочееМесто) Тогда
			ПараметрыСеанса.РабочееМесто = УстСотрудник.РабочееМесто;
		Иначе
			ПараметрыСеанса.РабочееМесто = слСомпьютерСсылка;
		КонецЕсли;
	Иначе
		ПараметрыСеанса.РабочееМесто = слСомпьютерСсылка;
	КонецЕсли;
	// Current style
	Если ЗначениеЗаполнено(УстСотрудник.НастройкиПользователя) Тогда
		слСтиль = УстСотрудник.НастройкиПользователя.ProgramAppearanceStyle;
		Если ЗначениеЗаполнено(слСтиль) Тогда
			ПараметрыСеанса.СтильОформления = слСтиль.Метаданные().ЗначенияПеречисления[Перечисления.ProgramAppearanceStyles.Индекс(слСтиль)].Имя;
		Иначе
			ПараметрыСеанса.СтильОформления = "Windows";
		КонецЕсли;
	Иначе
		ПараметрыСеанса.СтильОформления = "Windows";
	КонецЕсли;
	// Current language
	Попытка
		ПараметрыСеанса.ТекЛокализация = Справочники.Локализация[ТекущийЯзык().КодЯзыка];
	Исключение
		ПараметрыСеанса.ТекЛокализация = Справочники.Локализация.ПустаяСсылка();
	КонецПопытки;
	// Configuration name
	ПараметрыСеанса.ИмяКонфигурации = "Пансионат";
	ПараметрыСеанса.ПредставлениеКонфигурации = СокрЛП(ПараметрыСеанса.ИмяКонфигурации);
	
КонецПроцедуры 
